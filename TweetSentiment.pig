rawtweet = load 'tweets/tweets*' using PigStorage('|') as (time,ID, handle, name, blank1, blank2, blank3, blank4, blank5, text);
good = load 'good/good.txt' as (word);
bad = load 'bad/bad.txt' as (word);
tweet = foreach rawtweet generate time, ID, flatten(TOKENIZE(text)) as tweetword;
wordslow = foreach tweet generate time, ID, LOWER(tweetword) as tweetlow;
goodjoin = join wordslow by (tweetlow), good by (word) using 'replicated';
badjoin = join wordslow by (tweetlow), bad by (word) using 'replicated';
goodgroup = GROUP goodjoin by (time,ID);
badgroup = GROUP badjoin by (time,ID);
goodcount = foreach goodgroup generate group, COUNT($1) as num;
badcount = foreach badgroup generate group, (COUNT($1)*-1) as num;
combined = UNION goodcount, badcount;
comb = GROUP combined by $0;
combinedGroup = foreach comb generate group, SUM(($1.num));
goodEval = foreach combinedGroup generate $0, ($1>0?'good':'unsure') as sentiment;
badEval = foreach combinedGroup generate $0, ($1<0?'bad':'unsure') as sentiment;
goodcounts = filter goodEval by sentiment == 'good';
badcounts = filter badEval by sentiment == 'bad';
good_group = GROUP goodcounts ALL;
bad_group = GROUP badcounts ALL;
finalGood = foreach good_group generate COUNT(goodcounts);
finalBad = foreach bad_group generate COUNT(badcounts);
finalCounts = UNION finalGood, finalBad;
store finalCounts into 'finalCounts';
